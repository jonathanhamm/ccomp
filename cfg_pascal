<program> ->
	program id (<identifier_list>) ;
	<program'>
	<compound_statement>
	.
	{
        pushscope(id.entry)
        <program>.type := <identifier_list>.type
	}

<program'> ->
	<declarations> <dec>
	|
	<subprogram_declarations>
	|
	ε

<dec> ->
	<subprogram_declarations>
	|
	ε

<identifier_list> ->
	id <identifier_list'>
    {
        addarg(id.entry, integer)
        <identifier_list>.args := makelistf(integer)
        <identifier_list'>.args := <identifier_list>.args
    }

<identifier_list'> ->
	, id <identifier_list'>
	{
        addarg(id.entry, integer)
        listappend(<identifier_list'>.args, integer)
        <identifier_list'>[1].args := <identifier_list'>.args

	}
	|
	ε
    {
        <identifier_list'>.type := void
    }

<declarations> ->
	var id : <type> ; <declarations'> 
    {
        addtype(id.entry, <type>.type)
    }
<declarations'> ->
	var id : <type> ; <declarations'>
    {
        addtype(id.entry, <type>.type)
    }
	|
	ε

<type> ->
	<standard_type>
    {
        <type>.type := <standard_type>.type
    }
	|
	array [num..num] of <standard_type>
    {
        <type>.type := array(num[1].val .. num[2].val, <standard_type>.type)
    }

<standard_type> ->
	integer
    {
        <standard_type>.type := integer
    }
	|
	real
    { 
        <standard_type>.type := real
    }
	
<subprogram_declarations> ->
	<subprogram_declaration> ; <subprogram_declarations'>

<subprogram_declarations'> ->
	<subprogram_declaration> ; <subprogram_declarations'>
	|
	ε

<subprogram_declaration> ->
	<subprogram_head>
	<subprogram_declaration'>
	<compound_statement>
    {
        if <compound_statement>.type = void then
            popscope()
        end
    }
<subprogram_declaration'> ->
	<declarations> <dec>
	|
	<subprogram_declarations>
	|
	ε

<subprogram_head> ->
	procedure id <subprogram_head'>;
    {
        pushscope(id.entry)
        addtype(id.entry, <subprogram_head'>.type)
    }
<subprogram_head'> ->
	<arguments>
    {
        <subprogram_head'>.type := <arguments>.args
    }
	|
	ε
    {
        <subprogram_head'>.type := makelistf(void)
    }

<arguments> ->
	(<parameter_list>)
    {
        <arguments>.args := <parameter_list>.args
    }

<parameter_list> ->
	id : <type> <parameter_list'> 
    { 
        addarg(id.entry, <type>.type)
        <parameter_list>.args := makelistf(<type>.type)
        <parameter_list'>.args := <parameter_list>.args
    }
<parameter_list'> ->
	; id : <type> <parameter_list'> 
    { 
        addarg(id.entry, <type>.type)
        listappend(<parameter_list'>.args, <type>.type)
        <parameter_list'>[1].args := <parameter_list'>.args
    }
	|
	ε
    {
        <parameter_list'>.args := void
    }
	
<compound_statement> ->
	begin
	<compound_statement'>
	end
    {
        <compound_statement>.type := void
    }

<compound_statement'> ->
	<optional_statements>
	|
	ε
	
<optional_statements> ->
	<statement_list>

<statement_list> ->
	<statement> <statement_list'>
<statement_list'> ->
	; <statement> <statement_list'>
	|
	ε

<statement> ->
	<variable> assignop <expression> 
    {
        if <variable>.type = integer then
            if <expression>.type <> integer then
                error(assignop, "Incompatible assignment to integer type")
            end
        elif <variable>.type = real then 
            if (<expression>.type <> real) and (<expression>.type <> integer) then
                error(assignop, "Incompatible assigment to real type")
            end
        elif <variable>.type <> <expression>.type then
            error(assignop, "Incompatible types used in assignment")
        end
    }
	|
	<procedure_statement>
	|
	<compound_statement>
	|
	while <expression> do <statement>
	|
	if <expression> then <statement> <statement'>
<statement'> ->
	else <statement>
	|
	ε
	
<variable> ->
	id <variable'> 
    {
        if <variable'>.type = integer then
            <variable>.type := getarray(id)
        elif <variable'>.type = void then
            <variable>.type := lookup(id)
        else
            <variable>.type := getarray(id)
            error(id, "Non-integer type used in array index")
        end
    }
<variable'> ->
	[<expression>]
    {
        <variable'>.type := <expression>.type
        <variable'>.val := <expression>.val
    }
	|
	ε
    {
        <variable'>.type := void
    }

<procedure_statement> ->
	call id <procedure_statement'>
    {
        if lookup(id) = <procedure_statement'>.type then
            <procedure_statement>.type := void
        end
    }

<procedure_statement'> ->
	(<expression_list>)
    {
        <procedure_statement'>.type := <expression_list>.args
    }
	|
	ε
    {
        <procedure_statement'>.type := makelista(void)
    }
	
<expression_list> ->
	<expression> <expression_list'>
    {
        <expression_list>.args := makelista(<expression>.type)
        <expression_list'>.args := <expression_list>.args
    }
<expression_list'> ->
	, <expression> <expression_list'>
    {
        listappend(<expression_list'>.args, <expression>.type)
        <expression_list'>[1].args := <expression_list'>.args
    }
	|
	ε
    {
        <exprssion_list'>.args := void
    }
	
<expression> ->
	<simple_expression> <expression'>
    { 
        <expression'>.in := <simple_expression>.type
        <expression>.type := <simple_expression>.type
        if <expression'>.type <> void then
            <expression>.type := <expression'>.type
        end
    }
<expression'> ->
	relop <simple_expression>
    {
        if (<expression'>.in <> integer) and (<expression'>.in <> real) then
            error(relop, "Incompatible type used on left-hand side of relational operator")
        end
        if (<simple_expression>.type <> integer) and (<simple_expression>.type <> real) then
            error(relop, "Incompatible type used on right-hand side of relational operator")
        end
        <expresion'>.type := integer
    }
	|
	ε
    {
        <expression'>.type := void
    }
	
<simple_expression> ->
	<sign> <term> <simple_expression'> 
    {
        <simple_expression>.type := <term>.type
        <simple_expression'>.in := <term>.type
        if <simple_expression'>.type <> void then
            <simple_expression>.type := <simple_expression>.type
        end
    }
	|
	<term> <simple_expression'>
    {
        <simple_expression>.type := <term>.type
        <simple_expression'>.in := <term>.type
        if <simple_expression'>.type <> void then
            <simple_expression>.type := <simple_expression'>.type
        end
    }

<simple_expression'> ->
	addop <term> <simple_expression'>
    {
        if (<simple_expression'>.in = integer) and (<term>.type = integer) then
            <simple_expression'>[1].in := integer
            <simple_expression'>.type := integer
        elif (<simple_expression'>.in = integer) and (<term>.type = real) then
             <simple_expression'>[1].in := real
            <simple_expression'>.type := real
        elif (<simple_expression'>.in = real ) and (<term>.type = integer) then
             <simple_expression'>[1].in := real
            <simple_expression'>.type := real
        elif (<simple_expression'>.in = real) and (<term>.type = real) then
            <simple_expression'>[1].in := real
        else
            error(addop, "Invalid type used in addop expression")
        end

        if <simple_expression'>[1].type <> void then
            <simple_expression'>.type := <simple_expression'>[1].type
        end
    }
	|
	ε
    {
        <simple_expression'>.type := void
    }


<term> ->
	<factor> <term'>
	{
        <term'>.in := <factor>.type
        <term>.type := <factor>.type
        if <term'>.type <> void then
            <term>.type := <term'>.type
        end
	}
<term'> ->
        mulop <factor> <term'>
        {
            if mulop.entry = mod then
                if (<term'>.in <> integer) or (<factor>.type <> integer) then
                    error(mulop, "Expected integer operands for modulo operation")
                end
            elif (mulop.entry = "/") or (mulop.entry = "div") then
                <term'>.in := real
                <term'>[1].in := real
                <term>.type := real
            else
                if (<term'>.in = integer) and (<factor>.type = integer) then
                    <term'>[1].in := integer
                    <term'>.type := integer
                elif (<term'>.in = integer) and (<factor>.type = real) then
                    <term'>[1].in := real
                    <term'>.type := real
                elif (<term'>.in = real ) and (<factor>.type = integer) then
                    <term'>[1].in := real
                    <term'>.type := real
                else
                    error(mulop, "Invalid type used in mulop expression")
                end
            end

            if <term'>[1].type <> void then
                <term'>.type := <term'>[1].type
                print(<term'>[1].type)
            end
        }
        |
        ε
        {
            <term'>.type := void
        }

<factor> ->
	id <factor'>
    {
        if <factor'>.type = integer then
            <factor>.type := getarray(id)
        elif <factor'>.type = void then
            <factor>.type := lookup(id)
        else
            <factor>.type := getarray(id)
            error(id, "Non-integer type used in array index")
        end
    }
	|
	num
    {
        <factor>.type := num.type
    }
	|
	(<expression>)
    { 
        <factor>.type := <expression>.type
    }
	|
	not <factor>
    {
        <factor>.type := <factor>[1].type
        if <factor>.type <> integer then
            error(bob, "Incompatible type used in logical not")
            <factor>.type := integer
        end
    }
    
<factor'> ->
	[<expression>]
    {
        <factor'>.type := <expression>.type

        <factor'>.val := <expression>.val
    }
	|
	ε
    {
        <factor'>.type := void
    }
    
<sign> ->
	+ | -
