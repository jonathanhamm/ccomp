<program> ->
	program id \( <identifier_list> \) ;
	<program'>
	<compound_statement>
	\.
	{
        pushscope(id.entry)
	}

<program'> ->
	<declarations> <dec>
	{
    }
	|
	<subprogram_declarations>
	{}
	|
	ε

<dec> ->
	<subprogram_declarations>
	|
	ε

<identifier_list> ->
	id <identifier_list'>
	{
	}
<identifier_list'> ->
	, id <identifier_list'>
	{
	}
	|
	ε

<declarations> ->
	var id : <type> ; <declarations'> 
    {
        addtype(id.entry, <type>.type)
    }
<declarations'> ->
	var id : <type> ; <declarations'>
    {
        addtype(id.entry, <type>.type)
    }
	|
	ε

<type> ->
	<standard_type>
    {
        <type>.type := <standard_type>.type
    }
	|
	array \[ num \. \. num \] of <standard_type>
    {
        <type>.type := array(num[1].val .. num[2].val, <standard_type>.type)
    }

<standard_type> ->
	integer
    {
        <standard_type>.type := integer
    }
	|
	real
    { 
        <standard_type>.type := real
    }
	
<subprogram_declarations> ->
	<subprogram_declaration> ; <subprogram_declarations'>

<subprogram_declarations'> ->
	<subprogram_declaration> ; <subprogram_declarations'>
	|
	ε

<subprogram_declaration> ->
	<subprogram_head>
	<subprogram_declaration'>
	<compound_statement>
    {
        if <compound_statement>.type = void then
        fi
    }
<subprogram_declaration'> ->
	<declarations> <dec>
	|
	<subprogram_declarations>
	|
	ε

<subprogram_head> ->
	procedure id <subprogram_head'>;
    {
        pushscope(id.entry)
        addtype(id.entry, <subprogram_head'>.type)
        <subprogram_head>.scope := id.entry
    }
<subprogram_head'> ->
	<arguments>
    {
        <subprogram_head'>.type := <arguments>.args
    }
	|
	ε
    {
        <subprogram_head'>.type := void
    }

<arguments> ->
	\( <parameter_list> \)
    {
        <arguments>.args := <parameter_list>.args
    }
<parameter_list> ->
	id : <type> <parameter_list'> 
    { 
        addarg(id.entry, <type>.type)
        <parameter_list>.args := makelistf(<type>.type)
        <parameter_list'>.args := <parameter_list>.args
    }
<parameter_list'> ->
	; id : <type> <parameter_list'> 
    { 
        addarg(id.entry, <type>.type)
        listappend(<parameter_list'>.args, <type>.type)
        <parameter_list'>[1].args := <parameter_list'>.args
    }
	|
	ε
    {
        <parameter_list'>.args := void
    }
	
<compound_statement> ->
	begin
	<compound_statement'>
	end
    {
        <compound_statement>.type := void
    }

<compound_statement'> ->
	<optional_statements>
	|
	ε
	
<optional_statements> ->
	<statement_list>

<statement_list> ->
	<statement> <statement_list'>
<statement_list'> ->
	; <statement> <statement_list'>
	|
	ε

<statement> ->
	<variable> assignop <expression> 
    {
        if <variable>.type = integer then
            if <expression>.type <> integer then
                error(assignop, "Invalid assignment to integer type")
            fi
        elif <variable>.type = real then 
            if (<expression>.type <> real) and (<expression>.type <> integer) then
                error(assignop, "Invalid assigment to real type")
            fi
        else 
            error(assignop, "Invalid type used in assignment")
        fi
    }
	|
	<procedure_statement>
	|
	<compound_statement>
	|
	while <expression> do <statement>
	|
	if <expression> then <statement> <statement'>
<statement'> ->
	else <statement>
	|
	ε
	
<variable> ->
	id <variable'> 
    {
        if <variable'>.type = integer then
            <variable>.type := getarray(id)
        elif <variable'>.type = void then
            <variable>.type := lookup(id)
        else
            <variable>.type := getarray(id)
            error(id, "Non-integer type used in array index")
        fi
    }
<variable'> ->
	\[ <expression> \]
    {
        <variable'>.type := <expression>.type
        <variable'>.val := <expression>.val
    }
	|
	ε
    {
        <variable'>.type := void
    }

<procedure_statement> ->
	call id <procedure_statement'>
    {
        if lookup(id) = <procedure_statement'>.type then
        fi
    }
<procedure_statement'> ->
	\( <expression_list> \)
    {
        <procedure_statement'>.type := <expression_list>.args
    }
	|
	ε
    {
        <procedure_statement'>.type := void
    }
	
<expression_list> ->
	<expression> <expression_list'>
    {
        <expression_list>.args := makelista(<expression>.type)
        <expression_list'>.args := <expression_list>.args
    }
<expression_list'> ->
	, <expression> <expression_list'>
    {
        listappend(<expression_list'>.args, <expression>.type)
        <expression_list'>[1].args := <expression_list'>.args
    }
	|
	ε
    {
        <exprssion_list'>.args := void
    }
	
<expression> ->
	<simple_expression> <expression'>
    { 
        <expression'>.in := <simple_expression>.type
        <expression>.type := <simple_expression>.type
        if <expression'>.type <> void then
            <expression>.type := <expression'>.type
        fi
    }
<expression'> ->
	relop <simple_expression>
    {
        <expresion'>.type := integer
    }
	|
	ε
    {
        <expression'>.type := void
    }
	
<simple_expression> ->
	<sign> <term> <simple_expression'> 
    {
        <simple_expression>.type := <term>.type
        <simple_expression'>.in := <term>.type
        if <simple_expression'>.type <> void then
            <simple_expression>.type := <simple_expression>.type
        fi
    }
	|
	<term> <simple_expression'>
    {
        <simple_expression>.type := <term>.type
        <simple_expression'>.in := <term>.type
        if <simple_expression'>.type <> void then
            <simple_expression>.type := <simple_expression'>.type
        fi
    }

<simple_expression'> ->
	addop <term> <simple_expression'>
    {
        if (<simple_expression'>.in = integer) and (<term>.type = integer) then
            <simple_expression'>[1].in := integer
            <simple_expression'>.type := integer
        elif (<simple_expression'>.in = integer) and (<term>.type = real) then
             <simple_expression'>[1].in := real
            <simple_expression'>.type := real
        elif (<simple_expression'>.in = real ) and (<term>.type = integer) then
             <simple_expression'>[1].in := real
            <simple_expression'>.type := real
        elif (<simple_expression'>.in = real) and (<term>.type = real) then
            <simple_expression'>[1].in := real
        else
            error(addop, "Invalid type used in addop expression")
        fi

        if <simple_expression'>[1].type <> void then
            <simple_expression'>.type := <simple_expression'>[1].type
        fi
    }
	|
	ε
    {
        <simple_expression'>.type := void
    }


<term> ->
	<factor> <term'>
	{
        <term'>.in := <factor>.type
        <term>.type := <factor>.type
        if <term'>.type <> void then
            <term>.type := <term'>.type
        fi
	}
<term'> ->
        mulop <factor> <term'>
        {
            if mulop.entry = mod then
                if (<term'>.in <> integer) or (<factor>.type <> integer) then
                    error(mulop, "Expected integer operands for modulo operation")
                fi
            fi
            if (<term'>.in = integer) and (<factor>.type = integer) then
                <term'>[1].in := integer
                <term'>.type := integer
            elif (<term'>.in = integer) and (<factor>.type = real) then
                <term'>[1].in := real
                <term'>.type := real
            elif (<term'>.in = real ) and (<factor>.type = integer) then
                <term'>[1].in := real
                <term'>.type := real
            else
                error(mulop, "Invalid type used in mulop expression")
            fi

            if <term'>[1].type <> void then
                <term'>.type := <term'>[1].type
                print(<term'>[1].type)
            fi
        }
        |
        ε
        {
            <term'>.type := void
        }

<factor> ->
	id <factor'>
    {
        if <factor'>.type = integer then
            <factor>.type := getarray(id)
        elif <factor'>.type = void then
            <factor>.type := lookup(id)
        else
            <factor>.type := getarray(id)
            error(id, "Non-integer type used in array index")
        fi
    }
	|
	num
    {
        <factor>.type := num.type
    }
	|
	\( <expression> \)
    { 
        <factor>.type := <expression>.type
    }
	|
	not <factor>
    {
        <factor>.type := <factor>[1].type
    }
    
<factor'> ->
	\[ <expression> \]
    {
        <factor'>.type := <expression>.type

        <factor'>.val := <expression>.val
    }
	|
	ε
    {
        <factor'>.type := void
    }
    
<sign> ->
	\+ | \-
